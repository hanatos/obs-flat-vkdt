#version 460
#extension GL_GOOGLE_include_directive    : enable
#extension GL_EXT_nonuniform_qualifier    : enable

#include "shared.glsl"

layout(local_size_x = DT_LOCAL_SIZE_X, local_size_y = DT_LOCAL_SIZE_Y, local_size_z = 1) in;

layout(std140, set = 0, binding = 1) uniform params_t
{
  float sigma;
  float shadows;
  float highlights;
  float clarity;
} params;

layout( // input f16 buffer y
    set = 1, binding = 0
) uniform sampler2D img_in[];

layout( // output f16 buffer y blurred/downsized
    set = 1, binding = 1
) uniform writeonly image2D img_out[];

// gauss reduce, run on dimensions of reduced output buffer
void
main()
{
  ivec2 opos = ivec2(gl_GlobalInvocationID);
  int idx = int(gl_GlobalInvocationID.z);
  if(any(greaterThanEqual(opos, imageSize(img_out[idx])))) return;

  // blur, store only coarse res
#if 0 // 100ms (on intel hd5500)
  const float w[5] = {1.0f/16.0f, 4.0f/16.0f, 6.0f/16.0f, 4.0f/16.0f, 1.0f/16.0f};
  float y = 0.0f;
  for(int jj=-2;jj<=2;jj++) for(int ii=-2;ii<=2;ii++)
    y += texelFetch(img_in[idx], 2*opos+ivec2(ii,jj), 0).r * w[ii+2] * w[jj+2];
  imageStore(img_out[idx], opos, vec4(y));
#else
  vec2 ipos = (2*opos + vec2(0.5))/vec2(textureSize(img_in[idx], 0));
  // imageStore(img_out[idx], opos, sample_soft(img_in[idx], ipos)); // 40ms
  imageStore(img_out[idx], opos, sample_semisoft(img_in[idx], ipos)); // 25ms
#endif
}

